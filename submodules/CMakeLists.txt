add_subdirectory(emu2149)

include(FindPkgConfig OPTIONAL)

option(VENDORED_RTAUDIO "Prefer vendored RtAudio over system-installed one if one is found" OFF)

if(NOT VENDORED_RTAUDIO)
	if(PKG_CONFIG_FOUND)
		pkg_check_modules(RTAUDIO rtaudio>=5.1)
	endif(PKG_CONFIG_FOUND)

	if(NOT RTAUDIO_FOUND)
		find_library(RTAUDIO_MANUAL_LIBRARY rtaudio)
		find_path(RTAUDIO_MANUAL_INCLUDE_DIRS NAMES "RtAudio.h" PATH_SUFFIXES "include/rtaudio")

		if(RTAUDIO_MANUAL_LIBRARY AND RTAUDIO_MANUAL_INCLUDE_DIRS)
			include(CheckCXXSymbolExists)
			set(CMAKE_REQUIRED_INCLUDES ${RTAUDIO_MANUAL_INCLUDE_DIRS})
			set(CMAKE_REQUIRED_LIBRARIES ${RTAUDIO_MANUAL_LIBRARY})
			check_cxx_symbol_exists(RtAudio::getApiDisplayName "RtAudio.h" RTAUDIO_MANUAL_SUFFICIENT)
		endif(RTAUDIO_MANUAL_LIBRARY AND RTAUDIO_MANUAL_INCLUDE_DIRS)

		if(RTAUDIO_MANUAL_SUFFICIENT)
			set(RTAUDIO_FOUND TRUE)
			set(RTAUDIO_INCLUDE_DIRS ${RTAUDIO_MANUAL_INCLUDE_DIRS})
			set(RTAUDIO_LDFLAGS ${RTAUDIO_MANUAL_LIBRARY})
		endif(RTAUDIO_MANUAL_SUFFICIENT)
	endif(NOT RTAUDIO_FOUND)

	if(RTAUDIO_FOUND)
		include(FindThreads REQUIRED)
		list(APPEND RTAUDIO_LDFLAGS ${CMAKE_THREAD_LIBS_INIT})
		set(RTAUDIO_FOUND "${RTAUDIO_FOUND}" PARENT_SCOPE)
		set(RTAUDIO_INCLUDE_DIRS ${RTAUDIO_INCLUDE_DIRS} PARENT_SCOPE)
		set(RTAUDIO_LDFLAGS ${RTAUDIO_LDFLAGS} PARENT_SCOPE)
	endif(RTAUDIO_FOUND)
endif(NOT VENDORED_RTAUDIO)

if(NOT RTAUDIO_FOUND)
	add_subdirectory(RtAudio)
endif(NOT RTAUDIO_FOUND)

option(VENDORED_RTMIDI "Prefer vendored RtMidi over system-installed one if one is found" OFF)

if(NOT VENDORED_RTMIDI)
	if(PKG_CONFIG_FOUND)
		pkg_check_modules(RTMIDI rtmidi>=4.0)
	endif(PKG_CONFIG_FOUND)

	if(NOT RTMIDI_FOUND)
		find_library(RTMIDI_MANUAL_LIBRARY rtmidi)
		find_path(RTMIDI_MANUAL_INCLUDE_DIRS NAMES "RtMidi.h" PATH_SUFFIXES "include/rtmidi")

		if(RTAUDIO_MANUAL_LIBRARY AND RTAUDIO_MANUAL_INCLUDE_DIRS)
			set(RTMIDI_MANUAL_SUFFICIENT TRUE)
		endif(RTAUDIO_MANUAL_LIBRARY AND RTAUDIO_MANUAL_INCLUDE_DIRS)

		if(RTMIDI_MANUAL_SUFFICIENT)
			set(RTMIDI_FOUND TRUE)
			set(RTMIDI_INCLUDE_DIRS ${RTMIDI_MANUAL_INCLUDE_DIRS})
			set(RTMIDI_LDFLAGS ${RTMIDI_MANUAL_LIBRARY})
		endif(RTMIDI_MANUAL_SUFFICIENT)
	endif(NOT RTMIDI_FOUND)

	if(RTMIDI_FOUND)
		include(FindThreads REQUIRED)
		list(APPEND RTMIDI_LDFLAGS ${CMAKE_THREAD_LIBS_INIT})
		set(RTMIDI_FOUND "${RTMIDI_FOUND}" PARENT_SCOPE)
		set(RTMIDI_INCLUDE_DIRS ${RTMIDI_INCLUDE_DIRS} PARENT_SCOPE)
		set(RTMIDI_LDFLAGS ${RTMIDI_LDFLAGS} PARENT_SCOPE)
	endif(RTMIDI_FOUND)
endif(NOT VENDORED_RTMIDI)

if(NOT RTMIDI_FOUND)
	add_subdirectory(RtMidi)
endif(NOT RTMIDI_FOUND)
